
Функция ПолучитьПрокси(ТекстОшибки) Экспорт
		
	УстановитьПривилегированныйРежим(Истина);
	
	ТекстОшибки = "";
	Адрес = Константы.АдресСервераСервиса.Получить();
	КаталогСервиса = СтрЗаменить(Константы.КаталогСервиса.Получить(), "/", "");
	Адрес = Адрес + "/" + КаталогСервиса + "/ws/TechOperations/?wsdl";
	Пользователь = Константы.ПользовательСервиса.Получить();
	Пароль = Константы.ПарольКСервису.Получить();
	Попытка
		Определения = Новый WSОпределения(Адрес, Пользователь, Пароль,, 60);
	Исключение
		// Сообщим пользователю о том, что не получилось получить определение сервиса.
		ТекстОшибки = "Не удалось установить соединение с сервером. Повторите попытку позже.";
		Возврат Неопределено;
	КонецПопытки;
	URI = URIПространстваИменСервиса();
	Прокси = Новый WSПрокси(Определения, URI, "TechOperations", "TechOperationsSoap");
	Прокси.Пользователь = Пользователь;
	Прокси.Пароль = Пароль;
	
	Возврат Прокси;
	
КонецФункции

Функция URIПространстваИменСервиса()
	Возврат "http://techop.ru/ws";
КонецФункции
